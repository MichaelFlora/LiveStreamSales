plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt' // Kotlin annotation processor
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "tv.wfc.livestreamsales"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        project.ext.set("archivesBaseName", "LiveStreamSales-" + defaultConfig.versionName)
        resValue("string", "ym_app_scheme", applicationId)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures{
        viewBinding true
    }

    buildTypes {
        debug {
            debuggable true
            //minifyEnabled true
            buildConfigField("String", "YOO_KASSA_CLIENT_APPLICATION_KEY", "\"test_Nzk3NTQ1kYF1Pqhqr2tcjS7kBRC3hIaCffHRNPKktIg\"")
            buildConfigField("String", "YOO_KASSA_SHOP_ID", "\"797545\"")
            buildConfigField("boolean", "IgnoreRestRequests", "false")
            buildConfigField("boolean", "IgnoreRestHostNameVerification", "false")
        }

        release {
            debuggable false
            //minifyEnabled true
            buildConfigField("String", "YOO_KASSA_CLIENT_APPLICATION_KEY", "\"test_Nzk3NTQ1kYF1Pqhqr2tcjS7kBRC3hIaCffHRNPKktIg\"")
            buildConfigField("String", "YOO_KASSA_SHOP_ID", "\"797545\"")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Kotlin extensions
    implementation "androidx.core:core-ktx:$coreKtxVersion"

    // UI
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshLayoutVersion"
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "io.coil-kt:coil:$coilVersion"
    implementation 'com.github.romandanylyk:PageIndicatorView:b1bad589b5'
    implementation "com.tbuonomo:dotsindicator:$viewPagerDotsIndicatorVersion"
    implementation "com.github.bosphere.android-fadingedgelayout:fadingedgelayout:$fadingEdgeLayoutVersion"

    // Payment
    implementation "ru.yoomoney.sdk.kassa.payments:yookassa-android-sdk:$yookassaAndroidSdkVersion"
    implementation "ru.yoomoney.sdk.auth:auth:$yookassaAuthVersion"

    // Video player
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:extension-cronet:$exoPlayerVersion"

    // Video broadcasting
    implementation "com.github.pedroSG94.rtmp-rtsp-stream-client-java:rtplibrary:$rtplibraryVersion"

    // Android navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // ReactiveX
    implementation "io.reactivex.rxjava3:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava3:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava3:rxandroid:$rxAndroidVersion"
    implementation "com.jakewharton.rxbinding4:rxbinding-material:$rxBindingMaterialVersion"
    implementation "androidx.room:room-rxjava3:$roomVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionsVersion"

    // ReactiveX + Lifecycle
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleReactiveStreamsKtx"

    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutineVersion"

    // Dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly "javax.annotation:jsr250-api:$jsr250ApiVersion" // many of Dagger features are provided by java annotations

    // Permissions
    implementation "com.karumi:dexter:$dexterVersion"

    // Date + Time
    implementation "joda-time:joda-time:$androidJodaVersion"

    // Serialization
    implementation "com.google.code.gson:gson:$gsonVersion"

    // REST API
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-tls:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitConverterScalars"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"

    // TCP
    implementation "com.pusher:pusher-java-client:$pusherJavaClientVersion"

    // Local storage
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Google Play Services
    implementation "com.google.android.gms:play-services-base:$playServicesBaseVersion"

    // Tests
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"

    // Local libraries
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    // Modules
    implementation project(':Core')
    implementation project(':CodeView')
    implementation project(":ContentLoader")
    implementation project(':AutoFillTextView')
    implementation project(':EditTextFormatters')
    implementation project(':Tools')
    implementation project(':RecyclerViewItemDecorators')
    implementation project(":ValueButtonView")
}